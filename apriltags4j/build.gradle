plugins {
  id "io.github.tomtzook.gradle-cmake" version "1.2.1"
  id "java-library"
}

version = VERSION
group = GROUP

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()

    maven {
        url('https://frcmaven.wpi.edu/artifactory/release')
    }
}

dependencies {
    ext.YEAR = WPILIB_VERSION.split('\\.')[0]
    implementation group: "edu.wpi.first.thirdparty.frc${YEAR}.opencv", name: 'opencv-java', version: "$OPENCV_VERSION"
}

jar {
    from (cmake.outputDir.map {
        return it.asFileTree.matching {
            include '**/*.dll'
            include '**/*.so'
            exclude '**/CMakeFiles/**'

        }
    }) {
        into 'natives'
    }
}

if (rootProject.hasProperty('crosscompile')) {
    apply from: project.file('crosscompile.gradle')
}

def compileMachineTargets = [machines.host]
compileMachineTargets.addAll(machines.customMachines)

cmake {
    targets {
        apriltags_jni {
            cmakeLists.set(file('src/main/jni/CMakeLists.txt'))
            targetMachines.addAll(compileMachineTargets)
        }
    }
}

tasks.clean.dependsOn tasks.cmakeClean
tasks.jar.dependsOn tasks.cmakeBuild

apply from: project.file('publish.gradle')
